package com.cesar31.captchaweb.parser;

import com.cesar31.captchaweb.model.*;
import com.cesar31.captchaweb.control.*;
import java_cup.runtime.Symbol;
import java.util.ArrayList;
import java.util.HashMap;
import java.util.List;

class CaptchaParser;

parser code {:

	private List<Err> errors;
	private Operation op;
	private BuildTag tag;

	private Symbol cur_token;

	public CaptchaParser(CaptchaLex lex) {
		super(lex);
		this.op = new Operation();
		this.tag = new BuildTag(this);
		this.errors = new ArrayList<>();
	}

	public void syntax_error(Symbol cur_token) {
		this.cur_token = cur_token;

		List<String> expected = new ArrayList<>();
		List<Integer> tokens = expected_token_ids();

		System.out.printf("Se encontro: %s -> (%s), linea %d columna %d, se esperaba -> ", cur_token.value, symbl_name_from_id(cur_token.sym), cur_token.left, cur_token.right);
		for(Integer i : tokens) {
			expected.add(symbl_name_from_id(i));
			System.out.printf("%s, ", symbl_name_from_id(i));
		}
		System.out.println("");
	}

	public List<Err> getErrors() {
		return this.errors;
	}

	protected int error_sync_size() {
		return 1;
	}

:}

terminal Token GCIC, HEAD, TITLE, LINK, BODY, SPAM, INPUT, TXTAREA, SELECT, OPTION, DIV, IMG, BR, BUTTON, H1, PARAGRAPH, SCRIPT, HREF, BCKGRND, COLOR, FONTS;
terminal Token FONTF, ALIGN, TYPE, ID, NAME, COLS, ROW, CLASS, SRC, WIDTH, HEIGHT, ALT, CLICK, INTEGER, DECIMAL, BOOL, TRUE, FALSE;
terminal Token CHR, STR, ASC, DESC, LETPAR, LETIMPAR, REVERSE, RANDOM_C, RANDOM_N, ALERT, EXIT, GLOBAL, HEX, COLOUR, INTQ, PIXEL, PERCNTG, ID_, INT, DEC;
terminal Token GREATER, SMALLER, LBRACE, RBRACE, LBRACKET, RBRACKET, COLON, SEMI, EQEQ, NEQ, GRTREQ, SMLLREQ, OR, AND, NOT, PLUS, MINUS, TIMES, DIVIDE;
terminal Token URL, LPAREN, RPAREN, EQUAL, ROWS, FONTF_VALUE, QUOTE, ERROR;
terminal Token TYPE_VALUE, ALIGN_VALUE, CLASS_VALUE, COMMA, ON_LOAD, ONCLICK, IN, ID_V;
terminal Token STRING, CHAR, IF, THEN, ELSE, REPEAT, UNTIL, WHILE, THEN_WHILE, INIT, END;

non terminal head, title, option, br, script, scripting;
non terminal clgcic, clhead, cltitle, cllink, clbody, clspam, clinput, cltxtarea, clselect, cloption, cldiv, climg, clbutton, clh1, clparagraph, clscript;
non terminal c_gcic, c_title, c_link, c_spam, c_h1, c_button, c_paragraph, c_img, c_input, c_txtarea, c_option, make_option, c_select, c_div;
non terminal c_head, head_opt, h_opt, c_body, body_opt, b_opt, c_script;
non terminal make_script, make_var, stat, statement;
non terminal control_if, else_if, control_while, control_repeat, instruction, instructions, control, make_ins, type_instruction, assignment;

non terminal Variable s, t, u, v, control_stat, a, b, c, d, boolean_val;
non terminal List<Token> in;
non terminal Param color, url, cls_rws, w_h;
non terminal Boolean mode;
non terminal Token str, string, type_var, ini, pxl_per;
non terminal Parameter id, name, href_src, bckgrnd_clr, font_size, font_family, text_align, type, cols_rows, clss, width_height, alt, onclick, opt, param;
non terminal List<Parameter> params;
non terminal HashMap<Param, Parameter> paragraph, button, h1, div, img, body, spam, input, txtarea, select, link, gcic;


/* gramatica principal */
c_gcic ::=
				gcic c_head c_body clgcic
				;

c_head ::=
				head head_opt clhead
				;

head_opt ::=
				head_opt h_opt
				| h_opt;

h_opt ::=
				c_title
				| c_link
				;

c_body ::= body body_opt clbody
				;

body_opt ::=
				body_opt b_opt
				|
				;

b_opt ::=
				c_h1
				| c_paragraph
				|	c_spam
				| c_input
				| c_txtarea
				| c_select
				| c_img
				| c_button
				| c_div
				| br
				| c_script
				;

c_script ::=
				script make_script clscript
				;

make_script ::=
				make_script scripting
				|
				;

scripting ::=
				statement
				| assignment
				| control
				;

statement ::=
				type_var:t make_var SEMI
					{: System.out.println("Tipo: " + t.getValue()); :}
				;

assignment ::=
				ID_V:id EQUAL a:v1 SEMI
					{: System.out.println("Asignacion: " + id.getValue() + " = " + v1); :}
				;

type_var ::=
				INT:t {: RESULT = t; :}
				| STR:t {: RESULT = t; :}
				| DEC:t {: RESULT = t; :}
				| BOOL:t {: RESULT = t; :}
				| CHR:t {: RESULT = t; :}
				;

make_var ::=
				make_var COMMA stat
				| stat
				;

stat ::=
				mode ID_V:id EQUAL a:v1
					{: System.out.println(id.getValue() + " -> " + v1.toString()); :}
				| mode ID_V
				;

mode ::=
				GLOBAL:s
				{: RESULT = true; :}
				|
				{: RESULT = false; :}
				;

/* instruction */
make_ins ::=
				make_ins instructions
				| instructions
				;

instructions ::=
				control
				| instruction
				;

instruction ::=
				statement
				| assignment
				;
/* instruction */

/* init */
ini ::=
				INIT LBRACE COLON make_ins COLON RBRACE END
				;

type_instruction ::=
				instruction
				| ini
				;
/* init */

/* IF - WHILE- REPEAT */
control ::=
				control_if
				| control_while
				| control_repeat
				;

/* IF */
control_if ::=
				IF:i LPAREN a:b1 RPAREN THEN type_instruction else_if
					{: System.out.println("if -> " + b1 + ": " + i); :}
				;

else_if ::=
				ELSE:e IF LPAREN a:b1 RPAREN THEN type_instruction else_if
					{: System.out.println("else if -> " + b1 + ": " + e); :}
				| ELSE:e type_instruction
					{: System.out.println("else: " + e); :}
				|
				;
/* IF */

/* WHILE */
control_while ::=
				WHILE LPAREN a:b1 RPAREN THEN_WHILE type_instruction
					{: System.out.println("while -> " + b1); :}
				;
/* WHILE */

/* REPEAT */
control_repeat ::=
				REPEAT LPAREN control_stat:b1 RPAREN UNTIL LPAREN a:b2 RPAREN type_instruction
					{: System.out.println("repeat -> " + b1 + " until ->" + b2); :}
				;

control_stat ::=
				INT ID_V EQUAL a:b1 {: RESULT = b1; :}
				| ID_V EQUAL a:b1 {: RESULT = b1; :}
				;
/* REPEAT */
/* IF - WHILE- REPEAT */

/* Operaciones logicas y aritmeticas */
a ::=
        a:b1 OR b:b2 {: RESULT = op.or(b1, b2); :}
        | b:b1 {: RESULT = b1; :}
        ;

b ::=
        b:b1 AND d:b2 {: RESULT = op.and(b1, b2); :}
        | d:b1 {: RESULT = b1; :}
        ;

d ::=
				s:n1 SMALLER s:n2 {: RESULT = op.compare(n1, n2, Compare.SMALLER); :}
				| s:n1 GREATER s:n2 {: RESULT = op.compare(n1, n2, Compare.GREATER); :}
				| s:n1 GRTREQ s:n2 {: RESULT = op.compare(n1, n2, Compare.GREATER_OR_EQUAL); :}
				| s:n1 SMLLREQ s:n2 {: RESULT = op.compare(n1, n2, Compare.LESS_OR_EQUAL); :}
				| s:n1 EQEQ s:n2 {: RESULT = op.compare(n1, n2, Compare.EQUAL); :}
				| s:n1 NEQ s:n2 {: RESULT = op.compare(n1, n2, Compare.NOT_EQUAL); :}
				| s:n1 {: RESULT = n1; :}
				;

s ::=
				s:n1 PLUS t:n2 {: RESULT = op.sum(n1, n2); :}
				| s:n1 MINUS t:n2 {: RESULT = op.subtraction(n1, n2); :}
				| t:n1 {: RESULT = n1; :}
				;

t ::=
				t:n1 TIMES u:n2 {: RESULT = op.multiplication(n1, n2); :}
				| t:n1 DIVIDE u:n2 {: RESULT = op.division(n1, n2); :}
				| u:n1 {: RESULT = n1; :}
				;

u ::=
				MINUS c:n1 {: RESULT = op.uminus(n1); :}
				| c:n1 {: RESULT = n1; :}
				;

c ::=
        NOT v:b1 {: RESULT = op.not(b1); :}
        | v:b1 {: RESULT = b1; :}
        ;

v ::=
				INTEGER:n1 {: RESULT = new Variable(Var.INTEGER, n1.getValue()); :}
				| DECIMAL:n1 {: RESULT = new Variable(Var.DECIMAL, n1.getValue()); :}
				| string:s1 {: RESULT = new Variable(Var.STRING, s1.getValue()); :}
				| CHAR:ch1 {: RESULT = new Variable(Var.CHAR, ch1.getValue()); :}
				| boolean_val:b1 {: RESULT = b1; :}
				| LPAREN a:n1 RPAREN {: RESULT = n1; :}
				;
/* Operaciones logicas y aritmeticas */

/* string */
string ::=
				STRING:s1 {: RESULT = s1; :}
				| ID_:s1 {: RESULT = s1; :}
				| INTQ:s1 {: RESULT = s1; :}
				| FONTF_VALUE:s1 {: RESULT = s1; :}
				| ALIGN_VALUE:s1 {: RESULT = s1; :}
				| COLOUR:s1 {: RESULT = s1; :}
				| TYPE_VALUE:s1 {: RESULT = s1; :}
				| CLASS_VALUE:s1 {: RESULT = s1; :}
				| PIXEL:s1 {: RESULT = s1; :}
				| PERCNTG:s1 {: RESULT = s1; :}
				| ONCLICK:s1 {: RESULT = s1; :}
				;
/* string */

/* boolean */
boolean_val ::=
				TRUE:b1 {: RESULT = new Variable(Var.BOOLEAN, b1.getValue()); :}
				| FALSE:b1 {: RESULT = new Variable(Var.BOOLEAN, b1.getValue()); :}
				;
/* boolean */

c_title ::=
				title in:list cltitle
				{: System.out.println("Titulo: " + list); :}
				;

c_link ::=
				link cllink
				;

c_spam ::=
				spam in:list clspam
					{:
						System.out.println("Spam: " + list);
					:}
				;

c_h1 ::=
				h1 in:list clh1
					{:
						System.out.println("h1: " + list);
					:}
				;

c_paragraph ::=
				paragraph in:list clparagraph
					{: System.out.println("paragraph: " + list); :}
				;

c_input ::=
				input clinput
				;

c_txtarea ::=
				txtarea cltxtarea;

c_button ::=
				button in:list clbutton
					{: System.out.println("button: " + list); :}
				;

c_select ::=
				select make_option clselect
				;

make_option ::=
				make_option c_option
				|
				;

c_option ::=
				option in:list cloption
					{: System.out.println("option: " + list); :}
				;

c_img ::=
				img climg
				;

c_div ::=
				div body_opt cldiv;

/* contet for p and h1 */
in ::=
			in:list str:s
				{:
					list.add(s);
					RESULT = list;
				:}
			| /* lambda */
				{:
					List<Token> list = new ArrayList<>();
					RESULT = list;
				:}
			;

str ::=
			IN:s {: RESULT = s; :}
			| ID_V:s {: RESULT = s; :}
			;

/* Etiqueta de apertura <C_GCIC> */
gcic ::=
				SMALLER:t GCIC params:list GREATER
					{: RESULT = tag.getParameters(t, Tag.GCIC, list); :}
				| error GCIC params GREATER
				| SMALLER error params GREATER
				// | SMALLER GCIC params error
				;

/* <C_HEAD> */
head ::=
				SMALLER HEAD GREATER
				| error HEAD GREATER
				| SMALLER error GREATER
				| SMALLER HEAD error
				;

/* <C_TITLE> */
title ::=
				SMALLER:t TITLE GREATER
				| error TITLE GREATER
				// | SMALLER error GREATER
				| SMALLER TITLE error
				;

/* <C_LINK> */
link ::=
				SMALLER:t LINK params:list GREATER
					{: RESULT = tag.getParameters(t, Tag.LINK, list); :}
				| error LINK params GREATER
				// | SMALLER error params GREATER
				// | SMALLER LINK params error
				;

/* <C_BODY> */
body ::=
				SMALLER:t BODY params:list GREATER
					{: RESULT = tag.getParameters(t, Tag.BODY, list); :}
				| error BODY params GREATER
				| SMALLER error params GREATER
				// | SMALLER BODY params error
				;

/* <C_SPAM> */
spam ::=
				SMALLER:t SPAM params:list GREATER
					{: RESULT = tag.getParameters(t, Tag.SPAN, list); :}
				| error SPAM params GREATER
				// | SMALLER error params GREATER
				// | SMALLER SPAM params error
				;

/* <C_INPUT> */
input ::=
				SMALLER:t INPUT params:list GREATER
					{: RESULT = tag.getParameters(t, Tag.INPUT, list); :}
				| error INPUT params GREATER
				// | SMALLER error params GREATER
				// | SMALLER INPUT params error
				;

/* <C_TEXTAREA> */
txtarea ::=
				SMALLER:t TXTAREA params:list GREATER
					{: RESULT = tag.getParameters(t, Tag.TEXTAREA, list); :}
				| error TXTAREA params GREATER
				// | SMALLER error params GREATER
				// | SMALLER TXTAREA params error
				;

/* <C_SELECT> */
select ::=
				SMALLER:t SELECT params:list GREATER
					{: RESULT = tag.getParameters(t, Tag.SELECT, list); :}
				| error SELECT params GREATER
				// | SMALLER error params GREATER
				// | SMALLER SELECT params error
				;

/* <C_OPTION> */
option ::=
				SMALLER OPTION GREATER
				| error OPTION GREATER
				| SMALLER error GREATER
				| SMALLER OPTION error
				;

/* <C_DIV> */
div ::=
				SMALLER:t DIV params:list GREATER
					{: RESULT = tag.getParameters(t, Tag.DIV, list); :}
				| error DIV params GREATER
				// | SMALLER error params GREATER
				// | SMALLER DIV params error
				;

/* <C_IMG> */
img ::=
				SMALLER:t IMG params:list GREATER
					{: RESULT = tag.getParameters(t, Tag.IMG, list); :}
				| error IMG params GREATER
				// | SMALLER error params GREATER
				// | SMALLER IMG params error
				;

/* <C_BR> */
br ::=
				SMALLER BR GREATER
				| error BR GREATER
				// | SMALLER error GREATER
				| SMALLER BR error
				;

/* <C_BUTTON> */
button ::=
				SMALLER:t BUTTON params:list GREATER
					{: RESULT = tag.getParameters(t, Tag.BUTTON, list); :}
				| error BUTTON params GREATER
				// | SMALLER error params GREATER
				// | SMALLER BUTTON params error
				;

/* <C_H1> */
h1 ::=
				SMALLER:t H1 params:list GREATER
					{: RESULT = tag.getParameters(t, Tag.H1, list); :}
				| error H1 params GREATER
				| SMALLER error params GREATER
				// | SMALLER H1 params error
				;

/* <C_P> */
paragraph ::=
				SMALLER:t PARAGRAPH params:list GREATER
					{: RESULT = tag.getParameters(t, Tag.P, list); :}
				| error PARAGRAPH params GREATER
				// | SMALLER error params GREATER
				// | SMALLER PARAGRAPH params error
				;

/* <C_SCRIPTING> */
script ::=
				SMALLER SCRIPT GREATER
				| error SCRIPT GREATER
				;

/* parametros de las etiquetas */
params ::=
				params:list param:p
					{:
						list.add(p);
						RESULT = list;
					:}
				|
						{: RESULT = new ArrayList<Parameter>(); :}
				;

param ::=
				LBRACKET opt:p RBRACKET
					{: RESULT = p; :}
				| error opt RBRACKET
				| LBRACKET opt error
				;

opt ::=
				id:p {: RESULT = p; :}
				| name:p  {: RESULT = p; :}
				| href_src:p  {: RESULT = p; :}
				| bckgrnd_clr:p  {: RESULT = p; :}
				| font_size:p  {: RESULT = p; :}
				| font_family:p  {: RESULT = p; :}
				| text_align:p  {: RESULT = p; :}
				| type:p  {: RESULT = p; :}
				| cols_rows:p  {: RESULT = p; :}
				| clss:p  {: RESULT = p; :}
				| width_height:p  {: RESULT = p; :}
				| alt:p  {: RESULT = p; :}
				| onclick:p  {: RESULT = p; :}
				| error
				;

id ::=
				ID EQUAL ID_:s
					{: RESULT = new Parameter(Param.ID, s.getValue()); :}
				| error EQUAL ID_
				| ID error ID_
				| ID EQUAL error
				;

name ::=
				NAME EQUAL STRING:s
					{: RESULT = new Parameter(Param.NAME, s.getValue()); :}
				| error EQUAL STRING
				| NAME error STRING
				| NAME EQUAL error
				;

/* href/src */
href_src ::=
				url:p EQUAL URL:s
					{: RESULT = new Parameter(p, s.getValue()); :}
				| error EQUAL URL
				| url error URL
				| url EQUAL error
				;

/* href/src */
url ::=
				HREF
					{: RESULT = Param.HREF; :}
				| SRC
					{: RESULT = Param.SRC; :}
				;

/* background / color */
bckgrnd_clr ::=
				color:p EQUAL COLOUR:s
					{: RESULT = new Parameter(p, s.getValue()); :}
				| error EQUAL COLOUR
				| color error COLOUR
				| color EQUAL error
				;

/* background / color */
color ::=
				BCKGRND
					{: RESULT = Param.BACKGROUND; :}
				| COLOR
					{: RESULT = Param.COLOR; :}
				;

font_size ::=
				FONTS EQUAL PIXEL:s
					{: RESULT = new Parameter(Param.FONT_SIZE, s.getValue()); :}
				// | error EQUAL PIXEL
				| FONTS error PIXEL
				| FONTS EQUAL error
				;

font_family ::=
				FONTF EQUAL FONTF_VALUE:s
					{: RESULT = new Parameter(Param.FONT_FAMILY, s.getValue()); :}
				| error EQUAL FONTF_VALUE
				| FONTF error FONTF_VALUE
				| FONTF EQUAL error
				;

text_align ::=
				ALIGN EQUAL ALIGN_VALUE:s
					{: RESULT = new Parameter(Param.TEXT_ALIGN, s.getValue()); :}
				| error EQUAL ALIGN_VALUE
				| ALIGN error ALIGN_VALUE
				| ALIGN EQUAL error
				;

/* type for inputs */
type ::=
				TYPE EQUAL TYPE_VALUE:s
					{: RESULT = new Parameter(Param.TYPE, s.getValue()); :}
				| error EQUAL TYPE_VALUE
				| TYPE error TYPE_VALUE
				| TYPE EQUAL error
				;

cols_rows ::=
				cls_rws:p EQUAL INTQ:s
					{: RESULT = new Parameter(p, s.getValue()); :}
				| error EQUAL INTQ
				| cls_rws error INTQ
				| cls_rws EQUAL error
				;

cls_rws ::=
				COLS
					{: RESULT = Param.COLS; :}
				| ROWS
					{: RESULT = Param.ROWS; :}
				;

clss ::=
				CLASS EQUAL CLASS_VALUE:s
					{: RESULT = new Parameter(Param.CLASS, s.getValue()); :}
				| error EQUAL CLASS_VALUE
				| CLASS error CLASS_VALUE
				| CLASS EQUAL error
				;

/* width/height */
width_height ::=
				w_h:p EQUAL pxl_per:s
					{: RESULT = new Parameter(p, s.getValue()); :}
				| error EQUAL pxl_per
				| w_h error pxl_per
				| w_h EQUAL error
				;

w_h ::=
				WIDTH:p {: RESULT = Param.WIDTH; :}
				| HEIGHT:p {: RESULT = Param.HEIGHT; :}
				;

pxl_per ::=
				PIXEL:s
					{: RESULT = s; :}
				| PERCNTG:s
					{: RESULT = s; :}
				;

/* alt */
alt ::=
				ALT EQUAL STRING:s
					{: RESULT = new Parameter(Param.ALT, s.getValue()); :}
				// | error EQUAL STRING // alt ::= error ...  and name ::= error ...
				| ALT error STRING
				| ALT EQUAL error
				;

/* onclick */
onclick ::=
				CLICK EQUAL ONCLICK:s
					{: RESULT = new Parameter(Param.ONCLICK, s.getValue()); :}
				| error EQUAL ONCLICK
				| CLICK error ONCLICK
				| CLICK EQUAL error
				;
/* --------------- Etiquetas de cierre --------------- */
/* </C_GCIC> */
clgcic ::=
				SMALLER DIVIDE GCIC GREATER
				| error DIVIDE GCIC GREATER
				| SMALLER error GCIC GREATER
				| SMALLER DIVIDE error GREATER
				| SMALLER DIVIDE GCIC error
				;

/* </C_HEAD> */
clhead ::=
				SMALLER DIVIDE HEAD GREATER
				| error DIVIDE HEAD GREATER
				| SMALLER error HEAD GREATER
				| SMALLER DIVIDE error GREATER
				| SMALLER DIVIDE HEAD error
				;

/* </C_TITLE> */
cltitle ::=
				SMALLER DIVIDE TITLE GREATER
				| error DIVIDE TITLE GREATER
				| SMALLER error TITLE GREATER
				| SMALLER DIVIDE error GREATER
				| SMALLER DIVIDE TITLE error
				;

/* </C_LINK> */
cllink ::=
				SMALLER DIVIDE LINK GREATER
				| error DIVIDE LINK GREATER
				| SMALLER error LINK GREATER
				| SMALLER DIVIDE error GREATER
				| SMALLER DIVIDE LINK error
				;

/* </C_BODY> */
clbody ::=
				SMALLER DIVIDE BODY GREATER
				| error DIVIDE BODY GREATER
				| SMALLER error BODY GREATER
				| SMALLER DIVIDE error GREATER
				| SMALLER DIVIDE BODY error
				;

/* </C_SPAM> */
clspam ::=
				SMALLER DIVIDE SPAM GREATER
				| error DIVIDE SPAM GREATER
				| SMALLER error SPAM GREATER
				| SMALLER DIVIDE error GREATER
				| SMALLER DIVIDE SPAM error
				;

/* </C_INPUT> */
clinput ::=
				SMALLER DIVIDE INPUT GREATER
				| error DIVIDE INPUT GREATER
				| SMALLER error INPUT GREATER
				| SMALLER DIVIDE error GREATER
				| SMALLER DIVIDE INPUT error
				;

/* </C_TEXTAREA> */
cltxtarea ::=
				SMALLER DIVIDE TXTAREA GREATER
				| error DIVIDE TXTAREA GREATER
				| SMALLER error TXTAREA GREATER
				| SMALLER DIVIDE error GREATER
				| SMALLER DIVIDE TXTAREA error
				;

/* </C_SELECT> */
clselect ::=
				SMALLER DIVIDE SELECT GREATER
				| error DIVIDE SELECT GREATER
				| SMALLER error SELECT GREATER
				| SMALLER DIVIDE error GREATER
				| SMALLER DIVIDE SELECT error
				;

/* </C_OPTION> */
cloption ::=
				SMALLER DIVIDE OPTION GREATER
				| error DIVIDE OPTION GREATER
				| SMALLER error OPTION GREATER
				| SMALLER DIVIDE error GREATER
				| SMALLER DIVIDE OPTION error
				;

/* </C_DIV> */
cldiv ::=
				SMALLER DIVIDE DIV GREATER
				| error DIVIDE DIV GREATER
				| SMALLER error DIV GREATER
				| SMALLER DIVIDE error GREATER
				| SMALLER DIVIDE DIV error
				;

/* </C_IMG> */
climg ::=
				SMALLER DIVIDE IMG GREATER
				| error DIVIDE IMG GREATER
				| SMALLER error IMG GREATER
				| SMALLER DIVIDE error GREATER
				| SMALLER DIVIDE IMG error
				;

/* </C_BUTTON> */
clbutton ::=
				SMALLER DIVIDE BUTTON GREATER
				| error DIVIDE BUTTON GREATER
				| SMALLER error BUTTON GREATER
				| SMALLER DIVIDE error GREATER
				| SMALLER DIVIDE BUTTON error
				;

/* </C_H1> */
clh1 ::=
				SMALLER DIVIDE H1 GREATER
				| error DIVIDE H1 GREATER
				| SMALLER error H1 GREATER
				| SMALLER DIVIDE error GREATER
				| SMALLER DIVIDE H1 error
				;

/* </C_P> */
clparagraph ::=
				SMALLER DIVIDE PARAGRAPH GREATER
				| error DIVIDE PARAGRAPH GREATER
				| SMALLER error PARAGRAPH GREATER
				| SMALLER DIVIDE error GREATER
				| SMALLER DIVIDE PARAGRAPH error
				;

/* <C_SCRIPTING> */
clscript ::=
				SMALLER DIVIDE SCRIPT GREATER
				| error DIVIDE SCRIPT GREATER
				| SMALLER error SCRIPT GREATER
				| SMALLER DIVIDE error GREATER
				| SMALLER DIVIDE SCRIPT error
				;
