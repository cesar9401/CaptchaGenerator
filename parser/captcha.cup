package com.cesar31.captchaweb.parser;

import com.cesar31.captchaweb.model.*;
import com.cesar31.captchaweb.control.*;
import java_cup.runtime.Symbol;
import java.util.ArrayList;
import java.util.HashMap;
import java.util.List;

class CaptchaParser;

parser code {:

	private Operation operation;
	private Function function;
	private BuildTag tag;
	private ErrorHandler handler;

	private Enviroment e;
	private EnviromentHandler eh;

	private List<Err> errors;
	private Symbol cur_token;

	private Token type;

	public CaptchaParser(CaptchaLex lex) {
		super(lex);
		this.operation = new Operation(this);
		this.function = new Function(this);
		this.tag = new BuildTag(this);
		this.handler = new ErrorHandler(this);
		this.eh = new EnviromentHandler(this);

		this.errors = new ArrayList<>();
	}

	public void syntax_error(Symbol cur_token) {
		this.cur_token = cur_token;

		List<String> expected = new ArrayList<>();
		List<Integer> tokens = expected_token_ids();

		// System.out.printf("Se encontro: %s -> (%s), linea %d columna %d, se esperaba -> ", cur_token.value, symbl_name_from_id(cur_token.sym), cur_token.left, cur_token.right);
		for(Integer i : tokens) {
			expected.add(symbl_name_from_id(i));
			// System.out.printf("%s, ", symbl_name_from_id(i));
		}
		// System.out.println("");

		handler.getErrors((Token) cur_token.value, symbl_name_from_id(cur_token.sym), expected);
	}

	public List<Err> getErrors() {
		return this.errors;
	}

	protected int error_sync_size() {
		return 1;
	}

	private Token setQuote(Token t) {
		if(symbl_name_from_id(t.getType()).equals("CHAR")) {
			t.setValue("\'" + t.getValue() + "\'");
			return t;
		}

		t.setValue("\"" + t.getValue() +"\"");
		return t;
	}

:}

terminal Token GCIC, HEAD, TITLE, LINK, BODY, SPAM, INPUT, TXTAREA, SELECT, OPTION, DIV, IMG, BR, BUTTON, H1, PARAGRAPH, SCRIPT, HREF, BCKGRND, COLOR, FONTS;
terminal Token FONTF, ALIGN, TYPE, ID, NAME, COLS, CLASS, SRC, WIDTH, HEIGHT, ALT, CLICK, INTEGER, DECIMAL, BOOL, TRUE, FALSE;
terminal Token CHR, STR, ASC, DESC, LETPAR, LETIMPAR, REVERSE, RANDOM_C, RANDOM_N, ALERT, EXIT, GLOBAL, COLOUR, INTQ, PIXEL, PERCNTG, ID_, INT, DEC;
terminal Token GREATER, SMALLER, LBRACE, RBRACE, LBRACKET, RBRACKET, COLON, SEMI, EQEQ, NEQ, GRTREQ, SMLLREQ, OR, AND, NOT, PLUS, MINUS, TIMES, DIVIDE;
terminal Token URL, LPAREN, RPAREN, EQUAL, ROWS, FONTF_VALUE, ERROR;
terminal Token TYPE_VALUE, ALIGN_VALUE, CLASS_VALUE, COMMA, ON_LOAD, ONCLICK, IN, ID_V;
terminal Token STRING, CHAR, IF, THEN, ELSE, REPEAT, UNTIL, WHILE, THEN_WHILE, INIT, END, PROCESS;

non terminal scripting;
non terminal clgcic, clhead, cltitle, cllink, clbody, clspam, clinput, cltxtarea, clselect, cloption, cldiv, climg, clbutton, clh1, clparagraph, clscript;
non terminal make_script, make_var, stat, statement;
non terminal control_if, else_if, control_while, control_repeat, instruction, instructions, control, make_ins, type_instruction, assignment;
non terminal process;

non terminal Variable s, t, u, v, control_stat, a, b, c, d, boolean_val, function;
non terminal List<Token> in;
non terminal Param color, url, cls_rws, w_h;
non terminal Boolean mode;
non terminal Token str, string, type_var, ini, pxl_per;
non terminal Parameter id, name, href_src, bckgrnd_clr, font_size, font_family, text_align, type, cols_rows, clss, width_height, alt, onclick, opt, param;
non terminal List<Parameter> params;
non terminal HashMap<Param, Parameter> paragraph, button, h1, div, img, body, spam, input, txtarea, select, link, gcic, head, title, option, br, script;
non terminal Component c_script, c_title, c_link, c_spam, c_h1, c_button, c_paragraph, c_img, c_input, c_txtarea, c_select, b_opt, c_br, c_div, c_option, h_opt;
non terminal List<Component> body_opt, make_option, head_opt;
non terminal ComponentParent c_body, c_head;
non terminal Captcha c_gcic;

/* gramatica principal */
c_gcic ::=
				gcic:m
				c_head:head
				c_body:body clgcic
				{:
					RESULT = tag.makeCaptcha(Tag.GCIC, m, head, body);
				:}
				;

c_head ::=
				head:m head_opt:components clhead
				{:
					RESULT = tag.makeTagParent(Tag.HEAD, m, components);
				:}
				;

head_opt ::=
				head_opt:comps h_opt:c
					{:
						comps.add(c);
						RESULT = comps;
					:}
				| h_opt:c
				{:
					List<Component> comps = new ArrayList<>();
					comps.add(c);
					RESULT = comps;
				:}
				;

h_opt ::=
				c_title:c {: RESULT = c; :}
				| c_link:c {: RESULT = c; :}
				;

/* list -> List<Component> */
c_body ::= body:m body_opt:components clbody
					{:
						RESULT = tag.makeTagParent(Tag.BODY, m, components);
					:}
				;

body_opt ::=
				body_opt:list b_opt:c
					{:
						list.add(c);
						RESULT = list;
					:}
				|
					{: RESULT = new ArrayList<Component>(); :}
				;

b_opt ::=
				c_h1:c {: RESULT = c; :}
				| c_paragraph :c {: RESULT = c; :}
				|	c_spam :c {: RESULT = c; :}
				| c_input :c {: RESULT = c; :}
				| c_txtarea :c {: RESULT = c; :}
				| c_select :c {: RESULT = c; :}
				| c_img :c {: RESULT = c; :}
				| c_button :c {: RESULT = c; :}
				| c_div :c {: RESULT = c; :}
				| c_br :c {: RESULT = c; :}
				| c_script :c {: RESULT = c; :}
				;

c_script ::=
				script:m process /*make_script*/ clscript
					{:
						RESULT = tag.makeTag(Tag.SCRIPT, m, null);
					:}
				;

/* proceso */
process ::=
				PROCESS:p {: e = new Enviroment(p.getValue(), p.getLine(), p.getColumn(), e); :}
				LPAREN RPAREN LBRACKET make_script RBRACKET
					{: System.out.println("Proceso: " + p.getValue());  :}
				|
				;

make_script ::=
				make_script scripting
				|
				;

scripting ::=
				statement
				| assignment
				| control
				;

statement ::=
				type_var:t
				{: type = t; :}
				make_var SEMI
				;

assignment ::=
				ID_V:id EQUAL a:v1 SEMI
					{:
						eh.makeAssignment(id, v1, e);
					:}
				;

type_var ::=
				INT:t {: RESULT = t; :}
				| STR:t {: RESULT = t; :}
				| DEC:t {: RESULT = t; :}
				| BOOL:t {: RESULT = t; :}
				| CHR:t {: RESULT = t; :}
				;

make_var ::=
				make_var COMMA stat
				| stat
				;

stat ::=
				mode:b1 ID_V:id EQUAL a:v1
					{:
						/* Asignar variables y agregar a tabla de simbolos */
						eh.addSymbolTable(type, id, v1, b1, e, true);
					:}
				| mode:b1 ID_V:id
					{:
						/* Declarar variables y agregar a tabla de simbolos */
						eh.addSymbolTable(type, id, null, b1, e, false);
					:}
				;

mode ::=
				GLOBAL:s
				{: RESULT = true; :}
				|
				{: RESULT = false; :}
				;

/* instruction */
make_ins ::=
				make_ins instructions
				| instructions
				;

instructions ::=
				control
				| instruction
				;

instruction ::=
				statement
				| assignment
				;
/* init */
ini ::=
				INIT LBRACE COLON make_ins COLON RBRACE END
				;

type_instruction ::=
				instruction
				| ini
				;
/* init */

/* IF - WHILE- REPEAT */
control ::=
				control_if
				| control_while
				| control_repeat
				;

/* IF */
control_if ::=
				IF:i
					{: e = new Enviroment("IF", i.getLine(), i.getColumn(), e); :}
				LPAREN:l a:b1 RPAREN:r THEN
					{: eh.checkBooleanVariable("IF", l, b1, r); :}
				type_instruction
					{: e = e.getDad(); :}
				else_if
				;

else_if ::=
				ELSE:el
					{: e = new Enviroment("ELSE-IF", el.getLine(), el.getColumn(), e); :}
				IF LPAREN:l a:b1 RPAREN:r THEN
					{: eh.checkBooleanVariable("ELSE-IF", l, b1, r); :}
				type_instruction
					{: e = e.getDad(); :}
				else_if

				| ELSE:el
					{: e = new Enviroment("ELSE", el.getLine(), el.getColumn(), e); :}
				type_instruction
					{: e = e.getDad(); :}
				|
				;
/* IF */

/* WHILE */
control_while ::=
				WHILE:w
					{: e = new Enviroment("WHILE", w.getLine(), w.getColumn(), e); :}
				LPAREN:l a:b1 RPAREN:r THEN_WHILE
					{: eh.checkBooleanVariable("WHILE", l, b1, r); :}
				type_instruction
					{: e = e.getDad(); :}
				;
/* WHILE */

/* REPEAT */
control_repeat ::=
				REPEAT:r
					{: e = new Enviroment("REPEAT", r.getLine(), r.getColumn(), e); :}
				LPAREN control_stat:b1 RPAREN
				UNTIL LPAREN a:b2 RPAREN
					{:
						/* Revisar que b2 sea de tipo entero */
						if(b2 != null) {
							if(b2.getType() != Var.INTEGER) {
								System.out.println("Error");
							}
						}
					:}
				type_instruction
					{: e = e.getDad(); :}
				;

control_stat ::=
				INT:t ID_V:id EQUAL a:v1
					{:
						eh.checkForVariable(t, id, v1, e);
						RESULT = v1;
					:}
				| ID_V:id EQUAL a:v1
					{:
						eh.checkForVariable(null, id, v1, e);
						RESULT = v1;
					:}
				;
/* REPEAT */
/* IF - WHILE- REPEAT */

/* Operaciones logicas y aritmeticas */
a ::=
        a:b1 OR:op b:b2 {: RESULT = operation.or(b1, b2, op); :}
        | b:b1 {: RESULT = b1; :}
        ;

b ::=
        b:b1 AND:op d:b2 {: RESULT = operation.and(b1, b2, op); :}
        | d:b1 {: RESULT = b1; :}
        ;

d ::=
				s:n1 SMALLER:op s:n2 {: RESULT = operation.compare(n1, n2, Compare.SMALLER, op); :}
				| s:n1 GREATER:op s:n2 {: RESULT = operation.compare(n1, n2, Compare.GREATER, op); :}
				| s:n1 GRTREQ:op s:n2 {: RESULT = operation.compare(n1, n2, Compare.GREATER_OR_EQUAL, op); :}
				| s:n1 SMLLREQ:op s:n2 {: RESULT = operation.compare(n1, n2, Compare.LESS_OR_EQUAL, op); :}
				| s:n1 EQEQ:op s:n2 {: RESULT = operation.compare(n1, n2, Compare.EQUAL, op); :}
				| s:n1 NEQ:op s:n2 {: RESULT = operation.compare(n1, n2, Compare.NOT_EQUAL, op); :}
				| s:n1 {: RESULT = n1; :}
				;

s ::=
				s:n1 PLUS:op t:n2 {: RESULT = operation.sum(n1, n2, op); :}
				| s:n1 MINUS:op t:n2 {: RESULT = operation.subtraction(n1, n2, op); :}
				| t:n1 {: RESULT = n1; :}
				;

t ::=
				t:n1 TIMES:op u:n2 {: RESULT = operation.multiplication(n1, n2, op); :}
				| t:n1 DIVIDE:op u:n2 {: RESULT = operation.division(n1, n2, op); :}
				| u:n1 {: RESULT = n1; :}
				;

u ::=
				MINUS:op c:n1 {: RESULT = operation.uminus(n1, op); :}
				| c:n1 {: RESULT = n1; :}
				;

c ::=
        NOT:op function:b1 {: RESULT = operation.not(b1, op); :}
        | function:b1 {: RESULT = b1; :}
        ;

function ::=
				ASC LPAREN a:s1 RPAREN  {: RESULT = function.ASC(s1); :}
				| DESC LPAREN a:s1 RPAREN {: RESULT = function.DESC(s1); :}
				| LETPAR LPAREN a:s1 RPAREN {: RESULT = function.LETPAR_NUM(s1); :}
				| LETIMPAR LPAREN a:s1 RPAREN {: RESULT = function.LETIMPAR_NUM(s1); :}
				| REVERSE LPAREN a:s1 RPAREN {: RESULT = function.REVERSE(s1); :}
				| RANDOM_C LPAREN RPAREN {: RESULT = function.CARACTER_ALEATORIO(); :}
				| RANDOM_N LPAREN RPAREN {: RESULT = function.NUM_ALEATORIO(); :}
				| v:b1 {: RESULT = b1; :}
				;

v ::=
				INTEGER:n1 {: RESULT = new Variable(Var.INTEGER, n1.getValue()); :}
				| DECIMAL:n1 {: RESULT = new Variable(Var.DECIMAL, n1.getValue()); :}
				| string:s1 {: RESULT = new Variable(Var.STRING, s1.getValue()); :}
				| CHAR:ch1 {: RESULT = new Variable(Var.CHAR, ch1.getValue()); :}
				| ID_V:s {: RESULT = eh.getFromSymbolTable(s, e, true); :}
				| boolean_val:b1 {: RESULT = b1; :}
				| LPAREN a:n1 RPAREN {: RESULT = n1; :}
				;
/* Operaciones logicas y aritmeticas */

/* string */
string ::=
				STRING:s1 {: RESULT = s1; :}
				| ID_:s1 {: RESULT = s1; :}
				| INTQ:s1 {: RESULT = s1; :}
				| FONTF_VALUE:s1 {: RESULT = s1; :}
				| ALIGN_VALUE:s1 {: RESULT = s1; :}
				| COLOUR:s1 {: RESULT = s1; :}
				| TYPE_VALUE:s1 {: RESULT = s1; :}
				| CLASS_VALUE:s1 {: RESULT = s1; :}
				| PIXEL:s1 {: RESULT = s1; :}
				| PERCNTG:s1 {: RESULT = s1; :}
				| ONCLICK:s1 {: RESULT = s1; :}
				| URL:s1 {: RESULT = s1; :}
				;
/* string */

/* boolean */
boolean_val ::=
				TRUE:b1 {: RESULT = new Variable(Var.BOOLEAN, b1.getValue()); :}
				| FALSE:b1 {: RESULT = new Variable(Var.BOOLEAN, b1.getValue()); :}
				;
/* boolean */

c_title ::=
				title:m in:list cltitle
					{: RESULT = tag.makeTag(Tag.TITLE, m, list); :}
				;

c_link ::=
				link:m cllink
					{: RESULT = tag.makeTag(Tag.LINK, m, null); :}
				;

c_spam ::=
				spam:m in:list clspam
					{: RESULT = tag.makeTag(Tag.SPAN, m, list); :}
				;

c_h1 ::=
				h1:m in:list clh1
					{: RESULT = tag.makeTag(Tag.H1, m, list); :}
				;

c_paragraph ::=
				paragraph:m in:list clparagraph
					{: RESULT = tag.makeTag(Tag.P, m, list); :}
				;

c_input ::=
				input:m clinput
					{: RESULT = tag.makeTag(Tag.INPUT, m, null); :}
				;

c_txtarea ::=
				txtarea:m cltxtarea
					{: RESULT = tag.makeTag(Tag.TEXTAREA, m, null); :}
				;

c_button ::=
				button:m in:list clbutton
					{: RESULT = tag.makeTag(Tag.BUTTON, m, list); :}
				;

c_select ::=
				select:m make_option:options clselect
					{: RESULT = tag.makeTagParent(Tag.SELECT, m, options); :}
				;

make_option ::=
				make_option:list c_option:o
					{:
						list.add(o);
						RESULT = list;
					:}
				|
					{: RESULT = new ArrayList<Component>(); :}
				;

c_option ::=
				option:m in:list cloption
					{: RESULT = tag.makeTag(Tag.OPTION, m, list); :}
				;

c_img ::=
				img:m climg
					{: RESULT = tag.makeTag(Tag.IMG, m, null); :}
				;

c_br ::=
				br:m
					{: RESULT = tag.makeTag(Tag.BR, m, null); :}
				;

c_div ::=
				div:m body_opt:children cldiv
					{:
						/* Opciones para crear div */
						RESULT = tag.makeTagParent(Tag.DIV, m, children);
					:}
				;

/* contet for p and h1 */
in ::=
				in:list str:s
					{:
						list.add(s);
						RESULT = list;
					:}
				| /* lambda */
					{:
						List<Token> list = new ArrayList<>();
						RESULT = list;
					:}
				;

str ::=
				IN:s {: RESULT = s; :}
				| ID_V:s {: RESULT = s; :}
				| COLON:s {: RESULT = s; :}
				| INTEGER:s {: RESULT = s; :}
				| DECIMAL:s {: RESULT = s; :}
				| PLUS:s {: RESULT = s; :}
				| GCIC:s {: RESULT = s; :}
				| HEAD:s {: RESULT = s; :}
				| TITLE:s {: RESULT = s; :}
				| LINK:s {: RESULT = s; :}
				| BODY:s {: RESULT = s; :}
				| SPAM:s {: RESULT = s; :}
				| INPUT:s {: RESULT = s; :}
				| TXTAREA:s {: RESULT = s; :}
				| SELECT:s {: RESULT = s; :}
				| OPTION:s {: RESULT = s; :}
				| DIV:s {: RESULT = s; :}
				| IMG:s {: RESULT = s; :}
				| BR:s {: RESULT = s; :}
				| BUTTON:s {: RESULT = s; :}
				| PARAGRAPH:s {: RESULT = s; :}
				| SCRIPT:s {: RESULT = s; :}
				| HREF:s {: RESULT = s; :}
				| BCKGRND:s {: RESULT = s; :}
				| COLOR:s {: RESULT = s; :}
				| FONTS:s {: RESULT = s; :}
				| FONTF:s {: RESULT = s; :}
				| ALIGN:s {: RESULT = s; :}
				| TYPE:s {: RESULT = s; :}
				| ID:s {: RESULT = s; :}
				| NAME:s {: RESULT = s; :}
				| COLS:s {: RESULT = s; :}
				| ROWS:s {: RESULT = s; :}
				| CLASS:s {: RESULT = s; :}
				| SRC:s {: RESULT = s; :}
				| WIDTH:s {: RESULT = s; :}
				| HEIGHT:s {: RESULT = s; :}
				| ALT:s {: RESULT = s; :}
				| CLICK:s {: RESULT = s; :}
				| IF:s {: RESULT = s; :}
				| THEN:s {: RESULT = s; :}
				| ELSE:s {: RESULT = s; :}
				| REPEAT:s {: RESULT = s; :}
				| UNTIL:s {: RESULT = s; :}
				| THEN_WHILE:s {: RESULT = s; :}
				| INIT:s {: RESULT = s; :}
				| END:s {: RESULT = s; :}
				| INT:s {: RESULT = s; :}
				| DEC:s {: RESULT = s; :}
				| BOOL:s {: RESULT = s; :}
				| TRUE:s {: RESULT = s; :}
				| FALSE:s {: RESULT = s; :}
				| CHR:s {: RESULT = s; :}
				| STR:s {: RESULT = s; :}
				| DESC:s {: RESULT = s; :}
				| LETPAR:s {: RESULT = s; :}
				| LETIMPAR:s {: RESULT = s; :}
				| REVERSE:s {: RESULT = s; :}
				| RANDOM_C:s {: RESULT = s; :}
				| RANDOM_N:s {: RESULT = s; :}
				| ALERT:s {: RESULT = s; :}
				| EXIT:s {: RESULT = s; :}
				| GLOBAL:s {: RESULT = s; :}
				| ON_LOAD:s {: RESULT = s; :}
				| FONTF_VALUE:s {: RESULT = setQuote(s); :}
				| ALIGN_VALUE:s {: RESULT = setQuote(s); :}
				| COLOUR:s {: RESULT = setQuote(s); :}
				| TYPE_VALUE:s {: RESULT = setQuote(s); :}
				| CLASS_VALUE:s {: RESULT = setQuote(s); :}
				| INTQ:s {: RESULT = setQuote(s); :}
				| PIXEL:s {: RESULT = setQuote(s); :}
				| PERCNTG:s {: RESULT = setQuote(s); :}
				| ONCLICK:s {: RESULT = setQuote(s); :}
				| ID_:s {: RESULT = setQuote(s); :}
				| URL:s {: RESULT = setQuote(s); :}
				| STRING:s {: RESULT = setQuote(s); :}
				| CHAR:s {: RESULT = setQuote(s); :}
				// | SMALLER:s {: RESULT = s; :}
				| GREATER:s {: RESULT = s; :}
				| LBRACE:s {: RESULT = s; :}
				| RBRACE:s {: RESULT = s; :}
				| LBRACKET:s {: RESULT = s; :}
				| RBRACKET:s {: RESULT = s; :}
				| SEMI:s {: RESULT = s; :}
				| EQUAL:s {: RESULT = s; :}
				| EQEQ:s {: RESULT = s; :}
				| NEQ:s {: RESULT = s; :}
				| GRTREQ:s {: RESULT = s; :}
				| SMLLREQ:s {: RESULT = s; :}
				| OR:s {: RESULT = s; :}
				| AND:s {: RESULT = s; :}
				| NOT:s {: RESULT = s; :}
				| MINUS:s {: RESULT = s; :}
				| TIMES:s {: RESULT = s; :}
				| DIVIDE:s {: RESULT = s; :}
				| LPAREN:s {: RESULT = s; :}
				| RPAREN:s {: RESULT = s; :}
				| COMMA:s {: RESULT = s; :}
				;

/* Etiqueta de apertura <C_GCIC> */
gcic ::=
				SMALLER:t GCIC params:list GREATER
					{: RESULT = tag.getParameters(t, Tag.GCIC, list); :}
				| error GCIC params GREATER
				| SMALLER error params GREATER
				// | SMALLER GCIC params error
				;

/* <C_HEAD> */
head ::=
				SMALLER:t HEAD params:list GREATER
					{: RESULT = tag.getParameters(t, Tag.HEAD, list); :}
				| error HEAD params GREATER
				| SMALLER error params GREATER
				// | SMALLER HEAD params error
				;

/* <C_TITLE> */
title ::=
				SMALLER:t TITLE params:list GREATER
					{: RESULT = tag.getParameters(t, Tag.TITLE, list); :}
				| error TITLE params GREATER
				// | SMALLER error GREATER
				// | SMALLER TITLE params error
				;

/* <C_LINK> */
link ::=
				SMALLER:t LINK params:list GREATER
					{: RESULT = tag.getParameters(t, Tag.LINK, list); :}
				| error LINK params GREATER
				// | SMALLER error params GREATER
				// | SMALLER LINK params error
				;

/* <C_BODY> */
body ::=
				SMALLER:t BODY params:list GREATER
					{: RESULT = tag.getParameters(t, Tag.BODY, list); :}
				| error BODY params GREATER
				| SMALLER error params GREATER
				// | SMALLER BODY params error
				;

/* <C_SPAM> */
spam ::=
				SMALLER:t SPAM params:list GREATER
					{: RESULT = tag.getParameters(t, Tag.SPAN, list); :}
				| error SPAM params GREATER
				// | SMALLER error params GREATER
				// | SMALLER SPAM params error
				;

/* <C_INPUT> */
input ::=
				SMALLER:t INPUT params:list GREATER
					{: RESULT = tag.getParameters(t, Tag.INPUT, list); :}
				| error INPUT params GREATER
				// | SMALLER error params GREATER
				// | SMALLER INPUT params error
				;

/* <C_TEXTAREA> */
txtarea ::=
				SMALLER:t TXTAREA params:list GREATER
					{: RESULT = tag.getParameters(t, Tag.TEXTAREA, list); :}
				| error TXTAREA params GREATER
				// | SMALLER error params GREATER
				// | SMALLER TXTAREA params error
				;

/* <C_SELECT> */
select ::=
				SMALLER:t SELECT params:list GREATER
					{: RESULT = tag.getParameters(t, Tag.SELECT, list); :}
				| error SELECT params GREATER
				// | SMALLER error params GREATER
				// | SMALLER SELECT params error
				;

/* <C_OPTION> */
option ::=
				SMALLER:t OPTION params:list GREATER
					{: RESULT = tag.getParameters(t, Tag.OPTION, list); :}
				| error OPTION params GREATER
				| SMALLER error params GREATER
				// | SMALLER OPTION params error
				;

/* <C_DIV> */
div ::=
				SMALLER:t DIV params:list GREATER
					{: RESULT = tag.getParameters(t, Tag.DIV, list); :}
				| error DIV params GREATER
				// | SMALLER error params GREATER
				// | SMALLER DIV params error
				;

/* <C_IMG> */
img ::=
				SMALLER:t IMG params:list GREATER
					{: RESULT = tag.getParameters(t, Tag.IMG, list); :}
				| error IMG params GREATER
				// | SMALLER error params GREATER
				// | SMALLER IMG params error
				;

/* <C_BR> */
br ::=
				SMALLER:t BR params:list GREATER
					{: RESULT = tag.getParameters(t, Tag.BR, list); :}
				| error BR params GREATER
				// | SMALLER error GREATER
				// | SMALLER BR params error
				;

/* <C_BUTTON> */
button ::=
				SMALLER:t BUTTON params:list GREATER
					{: RESULT = tag.getParameters(t, Tag.BUTTON, list); :}
				| error BUTTON params GREATER
				// | SMALLER error params GREATER
				// | SMALLER BUTTON params error
				;

/* <C_H1> */
h1 ::=
				SMALLER:t H1 params:list GREATER
					{: RESULT = tag.getParameters(t, Tag.H1, list); :}
				| error H1 params GREATER
				| SMALLER error params GREATER
				// | SMALLER H1 params error
				;

/* <C_P> */
paragraph ::=
				SMALLER:t PARAGRAPH params:list GREATER
					{: RESULT = tag.getParameters(t, Tag.P, list); :}
				| error PARAGRAPH params GREATER
				// | SMALLER error params GREATER
				// | SMALLER PARAGRAPH params error
				;

/* <C_SCRIPTING> */
script ::=
				SMALLER:t SCRIPT params:list GREATER
					{: RESULT = tag.getParameters(t, Tag.SCRIPT, list); :}
				| error SCRIPT params GREATER
				// | SMALLER error params GREATER
				// | SMALLER SCRIPT params error
				;

/* parametros de las etiquetas */
params ::=
				params:list param:p
					{:
						list.add(p);
						RESULT = list;
					:}
				|
						{: RESULT = new ArrayList<Parameter>(); :}
				;

param ::=
				LBRACKET opt:p RBRACKET
					{: RESULT = p; :}
				| error opt RBRACKET
				| LBRACKET opt error
				;

opt ::=
				id:p {: RESULT = p; :}
				| name:p  {: RESULT = p; :}
				| href_src:p  {: RESULT = p; :}
				| bckgrnd_clr:p  {: RESULT = p; :}
				| font_size:p  {: RESULT = p; :}
				| font_family:p  {: RESULT = p; :}
				| text_align:p  {: RESULT = p; :}
				| type:p  {: RESULT = p; :}
				| cols_rows:p  {: RESULT = p; :}
				| clss:p  {: RESULT = p; :}
				| width_height:p  {: RESULT = p; :}
				| alt:p  {: RESULT = p; :}
				| onclick:p  {: RESULT = p; :}
				| error
				;

id ::=
				ID EQUAL ID_:s
					{: RESULT = new Parameter(Param.ID, s.getValue()); :}
				| error EQUAL ID_
				| ID error ID_
				| ID EQUAL error
				;

name ::=
				NAME EQUAL STRING:s
					{: RESULT = new Parameter(Param.NAME, s.getValue()); :}
				| error EQUAL STRING
				| NAME error STRING
				| NAME EQUAL error
				;

/* href/src */
href_src ::=
				url:p EQUAL URL:s
					{: RESULT = new Parameter(p, s.getValue()); :}
				| error EQUAL URL
				| url error URL
				| url EQUAL error
				;

/* href/src */
url ::=
				HREF
					{: RESULT = Param.HREF; :}
				| SRC
					{: RESULT = Param.SRC; :}
				;

/* background / color */
bckgrnd_clr ::=
				color:p EQUAL COLOUR:s
					{: RESULT = new Parameter(p, s.getValue()); :}
				| error EQUAL COLOUR
				| color error COLOUR
				| color EQUAL error
				;

/* background / color */
color ::=
				BCKGRND
					{: RESULT = Param.BACKGROUND; :}
				| COLOR
					{: RESULT = Param.COLOR; :}
				;

font_size ::=
				FONTS EQUAL PIXEL:s
					{: RESULT = new Parameter(Param.FONT_SIZE, s.getValue()); :}
				// | error EQUAL PIXEL
				| FONTS error PIXEL
				| FONTS EQUAL error
				;

font_family ::=
				FONTF EQUAL FONTF_VALUE:s
					{: RESULT = new Parameter(Param.FONT_FAMILY, s.getValue()); :}
				| error EQUAL FONTF_VALUE
				| FONTF error FONTF_VALUE
				| FONTF EQUAL error
				;

text_align ::=
				ALIGN EQUAL ALIGN_VALUE:s
					{: RESULT = new Parameter(Param.TEXT_ALIGN, s.getValue()); :}
				| error EQUAL ALIGN_VALUE
				| ALIGN error ALIGN_VALUE
				| ALIGN EQUAL error
				;

/* type for inputs */
type ::=
				TYPE EQUAL TYPE_VALUE:s
					{: RESULT = new Parameter(Param.TYPE, s.getValue()); :}
				| error EQUAL TYPE_VALUE
				| TYPE error TYPE_VALUE
				| TYPE EQUAL error
				;

cols_rows ::=
				cls_rws:p EQUAL INTQ:s
					{: RESULT = new Parameter(p, s.getValue()); :}
				| error EQUAL INTQ
				| cls_rws error INTQ
				| cls_rws EQUAL error
				;

cls_rws ::=
				COLS
					{: RESULT = Param.COLS; :}
				| ROWS
					{: RESULT = Param.ROWS; :}
				;

clss ::=
				CLASS EQUAL CLASS_VALUE:s
					{: RESULT = new Parameter(Param.CLASS, s.getValue()); :}
				| error EQUAL CLASS_VALUE
				| CLASS error CLASS_VALUE
				| CLASS EQUAL error
				;

/* width/height */
width_height ::=
				w_h:p EQUAL pxl_per:s
					{: RESULT = new Parameter(p, s.getValue()); :}
				| error EQUAL pxl_per
				| w_h error pxl_per
				| w_h EQUAL error
				;

w_h ::=
				WIDTH:p {: RESULT = Param.WIDTH; :}
				| HEIGHT:p {: RESULT = Param.HEIGHT; :}
				;

pxl_per ::=
				PIXEL:s
					{: RESULT = s; :}
				| PERCNTG:s
					{: RESULT = s; :}
				;

/* alt */
alt ::=
				ALT EQUAL STRING:s
					{: RESULT = new Parameter(Param.ALT, s.getValue()); :}
				// | error EQUAL STRING // alt ::= error ...  and name ::= error ...
				| ALT error STRING
				| ALT EQUAL error
				;

/* onclick */
onclick ::=
				CLICK EQUAL ONCLICK:s
					{: RESULT = new Parameter(Param.ONCLICK, s.getValue()); :}
				| error EQUAL ONCLICK
				| CLICK error ONCLICK
				| CLICK EQUAL error
				;
/* --------------- Etiquetas de cierre --------------- */
/* </C_GCIC> */
clgcic ::=
				SMALLER DIVIDE GCIC GREATER
				| error DIVIDE GCIC GREATER
				| SMALLER error GCIC GREATER
				| SMALLER DIVIDE error GREATER
				| SMALLER DIVIDE GCIC error
				;

/* </C_HEAD> */
clhead ::=
				SMALLER DIVIDE HEAD GREATER
				| error DIVIDE HEAD GREATER
				| SMALLER error HEAD GREATER
				| SMALLER DIVIDE error GREATER
				| SMALLER DIVIDE HEAD error
				;

/* </C_TITLE> */
cltitle ::=
				SMALLER DIVIDE TITLE GREATER
				| error DIVIDE TITLE GREATER
				| SMALLER error TITLE GREATER
				| SMALLER DIVIDE error GREATER
				| SMALLER DIVIDE TITLE error
				;

/* </C_LINK> */
cllink ::=
				SMALLER DIVIDE LINK GREATER
				| error DIVIDE LINK GREATER
				| SMALLER error LINK GREATER
				| SMALLER DIVIDE error GREATER
				| SMALLER DIVIDE LINK error
				;

/* </C_BODY> */
clbody ::=
				SMALLER DIVIDE BODY GREATER
				| error DIVIDE BODY GREATER
				| SMALLER error BODY GREATER
				| SMALLER DIVIDE error GREATER
				| SMALLER DIVIDE BODY error
				;

/* </C_SPAM> */
clspam ::=
				SMALLER DIVIDE SPAM GREATER
				| error DIVIDE SPAM GREATER
				| SMALLER error SPAM GREATER
				| SMALLER DIVIDE error GREATER
				| SMALLER DIVIDE SPAM error
				;

/* </C_INPUT> */
clinput ::=
				SMALLER DIVIDE INPUT GREATER
				| error DIVIDE INPUT GREATER
				| SMALLER error INPUT GREATER
				| SMALLER DIVIDE error GREATER
				| SMALLER DIVIDE INPUT error
				;

/* </C_TEXTAREA> */
cltxtarea ::=
				SMALLER DIVIDE TXTAREA GREATER
				| error DIVIDE TXTAREA GREATER
				| SMALLER error TXTAREA GREATER
				| SMALLER DIVIDE error GREATER
				| SMALLER DIVIDE TXTAREA error
				;

/* </C_SELECT> */
clselect ::=
				SMALLER DIVIDE SELECT GREATER
				| error DIVIDE SELECT GREATER
				| SMALLER error SELECT GREATER
				| SMALLER DIVIDE error GREATER
				| SMALLER DIVIDE SELECT error
				;

/* </C_OPTION> */
cloption ::=
				SMALLER DIVIDE OPTION GREATER
				| error DIVIDE OPTION GREATER
				| SMALLER error OPTION GREATER
				| SMALLER DIVIDE error GREATER
				| SMALLER DIVIDE OPTION error
				;

/* </C_DIV> */
cldiv ::=
				SMALLER DIVIDE DIV GREATER
				| error DIVIDE DIV GREATER
				| SMALLER error DIV GREATER
				| SMALLER DIVIDE error GREATER
				| SMALLER DIVIDE DIV error
				;

/* </C_IMG> */
climg ::=
				SMALLER DIVIDE IMG GREATER
				| error DIVIDE IMG GREATER
				| SMALLER error IMG GREATER
				| SMALLER DIVIDE error GREATER
				| SMALLER DIVIDE IMG error
				;

/* </C_BUTTON> */
clbutton ::=
				SMALLER DIVIDE BUTTON GREATER
				| error DIVIDE BUTTON GREATER
				| SMALLER error BUTTON GREATER
				| SMALLER DIVIDE error GREATER
				| SMALLER DIVIDE BUTTON error
				;

/* </C_H1> */
clh1 ::=
				SMALLER DIVIDE H1 GREATER
				| error DIVIDE H1 GREATER
				| SMALLER error H1 GREATER
				| SMALLER DIVIDE error GREATER
				| SMALLER DIVIDE H1 error
				;

/* </C_P> */
clparagraph ::=
				SMALLER DIVIDE PARAGRAPH GREATER
				| error DIVIDE PARAGRAPH GREATER
				| SMALLER error PARAGRAPH GREATER
				| SMALLER DIVIDE error GREATER
				| SMALLER DIVIDE PARAGRAPH error
				;

/* <C_SCRIPTING> */
clscript ::=
				SMALLER DIVIDE SCRIPT GREATER
				| error DIVIDE SCRIPT GREATER
				| SMALLER error SCRIPT GREATER
				| SMALLER DIVIDE error GREATER
				| SMALLER DIVIDE SCRIPT error
				;
