package com.cesar31.captchaweb.parser;

import com.cesar31.captchaweb.model.*;
import com.cesar31.captchaweb.control.*;
import java_cup.runtime.Symbol;
import java.util.ArrayList;
import java.util.List;

class CaptchaParser;

parser code {:

	private Operation op;

	private Symbol cur_token;

	public CaptchaParser(CaptchaLex lex) {
		super(lex);
		this.op = new Operation();
	}

	public void syntax_error(Symbol cur_token) {
		this.cur_token = cur_token;

		List<String> expected = new ArrayList<>();
		List<Integer> tokens = expected_token_ids();

		System.out.printf("Se encontro: %s -> (%s), linea %d columna %d, se esperaba -> ", cur_token.value, symbl_name_from_id(cur_token.sym), cur_token.left, cur_token.right);
		for(Integer i : tokens) {
			expected.add(symbl_name_from_id(i));
			System.out.printf("%s, ", symbl_name_from_id(i));
		}
		System.out.println("");
	}

	protected int error_sync_size() {
		return 1;
	}

:}

terminal Token GCIC, HEAD, TITLE, LINK, BODY, SPAM, INPUT, TXTAREA, SELECT, OPTION, DIV, IMG, BR, BUTTON, H1, PARAGRAPH, SCRIPT, HREF, BCKGRND, COLOR, FONTS;
terminal Token FONTF, ALIGN, ALING_VALUE, TYPE, ID, NAME, COLS, ROW, CLASS, SRC, WIDTH, HEIGHT, ALT, CLICK, INTEGER, DECIMAL, BOOL, TRUE, FALSE;
terminal Token CHR, STR, ASC, DESC, LETPAR, LETIMPAR, REVERSE, RANDOM_C, RANDOM_N, ALERT, EXIT, GLOBAL, HEX, COLOUR, INTQ, PIXEL, PERCENTG, ID_, INT, DEC;
terminal Token GREATER, SMALLER, LBRACE, RBRACE, LBRACKET, RBRACKET, COLON, SEMI, EQEQ, NEQ, GRTREQ, SMLLREQ, OR, AND, NOT, PLUS, MINUS, TIMES, DIVIDE;
terminal Token URL, LPAREN, RPAREN, EQUAL, PERCNTG, ROWS, FONTF_VALUE, QUOTE, ERROR;
terminal Token TYPE_VALUE, ALIGN_VALUE, CLASS_VALUE, COMMA;
terminal Token ON_LOAD, ONCLICK, IN, ID_V;
terminal Token STRING;
terminal Token CHAR;

non terminal id, name, opt, gcic;
non terminal params, head, title, link, bckgrnd_clr, font_size, font_family;
non terminal text_align, type, cls_rws, cols_rows, clss, url, href_src, width_height, w_h, pxl_per;
non terminal alt, body, spam, input, txtarea, select, option, div, img, br, button, h1, paragraph, script, scripting;
non terminal param, onclick;
non terminal clgcic, clhead, cltitle, cllink, clbody, clspam, clinput, cltxtarea, clselect, cloption, cldiv, climg, clbutton, clh1, clparagraph, clscript;
non terminal c_gcic, c_title, c_link, c_spam, c_h1, c_button, c_paragraph, c_img, c_input, c_txtarea, c_option, make_option, c_select, c_div;
non terminal c_head, head_opt, h_opt, c_body, body_opt, b_opt, c_script;
non terminal make_script, make_var, stat, statement;

non terminal Variable s, t, u, v;
non terminal Variable a, b, c, d, boolean_val;
non terminal List<Token> in;
non terminal String color;
non terminal Boolean mode;
non terminal Token str, string, type_var;

/* gramatica principal */
c_gcic ::=
				gcic c_head c_body clgcic
				;

c_head ::=
				head head_opt clhead
				;

head_opt ::=
				head_opt h_opt
				| h_opt;

h_opt ::=
				c_title
				| c_link
				;

c_body ::= body body_opt clbody
				;

body_opt ::=
				body_opt b_opt
				|
				;

b_opt ::=
				c_h1
				| c_paragraph
				|	c_spam
				| c_input
				| c_txtarea
				| c_select
				| c_img
				| c_button
				| c_div
				| br
				| c_script
				;

c_script ::=
				script make_script clscript
				;

make_script ::=
				make_script scripting
				|
				;

scripting ::=
				statement
				;

statement ::=
				type_var:t make_var SEMI
					{: System.out.println("Tipo: " + t.getValue()); :}
				;

type_var ::=
				INT:t {: RESULT = t; :}
				| STR:t {: RESULT = t; :}
				| DEC:t {: RESULT = t; :}
				| BOOL:t {: RESULT = t; :}
				| CHR:t {: RESULT = t; :}
				;

make_var ::=
				make_var COMMA stat
				| stat
				;

stat ::=
				mode ID_V:id EQUAL a:v1
					{: System.out.println(id.getValue() + " -> " + v1.toString()); :}
				| mode ID_V
				;

mode ::=
				GLOBAL:s
				{: RESULT = true; :}
				|
				{: RESULT = false; :}
				;

/* Operaciones logicas y aritmeticas */
a ::=
        a:b1 OR b:b2 {: RESULT = op.or(b1, b2); :}
        | b:b1 {: RESULT = b1; :}
        ;

b ::=
        b:b1 AND d:b2 {: RESULT = op.and(b1, b2); :}
        | d:b1 {: RESULT = b1; :}
        ;

d ::=
				s:n1 SMALLER s:n2 {: RESULT = op.compare(n1, n2, Compare.SMALLER); :}
				| s:n1 GREATER s:n2 {: RESULT = op.compare(n1, n2, Compare.GREATER); :}
				| s:n1 GRTREQ s:n2 {: RESULT = op.compare(n1, n2, Compare.GREATER_OR_EQUAL); :}
				| s:n1 SMLLREQ s:n2 {: RESULT = op.compare(n1, n2, Compare.LESS_OR_EQUAL); :}
				| s:n1 EQEQ s:n2 {: RESULT = op.compare(n1, n2, Compare.EQUAL); :}
				| s:n1 NEQ s:n2 {: RESULT = op.compare(n1, n2, Compare.NOT_EQUAL); :}
				| s:n1 {: RESULT = n1; :}
				;

s ::=
				s:n1 PLUS t:n2 {: RESULT = op.sum(n1, n2); :}
				| s:n1 MINUS t:n2 {: RESULT = op.subtraction(n1, n2); :}
				| t:n1 {: RESULT = n1; :}
				;

t ::=
				t:n1 TIMES u:n2 {: RESULT = op.multiplication(n1, n2); :}
				| t:n1 DIVIDE u:n2 {: RESULT = op.division(n1, n2); :}
				| u:n1 {: RESULT = n1; :}
				;

u ::=
				MINUS c:n1 {: RESULT = op.uminus(n1); :}
				| c:n1 {: RESULT = n1; :}
				;

c ::=
        NOT v:b1 {: RESULT = op.not(b1); :}
        | v:b1 {: RESULT = b1; :}
        ;

v ::=
				INTEGER:n1 {: RESULT = new Variable(Var.INTEGER, n1.getValue()); :}
				| DECIMAL:n1 {: RESULT = new Variable(Var.DECIMAL, n1.getValue()); :}
				| string:s1 {: RESULT = new Variable(Var.STRING, s1.getValue()); :}
				| CHAR:ch1 {: RESULT = new Variable(Var.CHAR, ch1.getValue()); :}
				| boolean_val:b1 {: RESULT = b1; :}
				| LPAREN a:n1 RPAREN {: RESULT = n1; :}
				;
/* Operaciones logicas y aritmeticas */

/* string */
string ::=
				STRING:s1 {: RESULT = s1; :}
				| ID_:s1 {: RESULT = s1; :}
				;
/* string */

/* boolean */
boolean_val ::=
				TRUE:b1 {: RESULT = new Variable(Var.BOOLEAN, b1.getValue()); :}
				| FALSE:b1 {: RESULT = new Variable(Var.BOOLEAN, b1.getValue()); :}
				;
/* boolean */

c_title ::=
				title in:list cltitle
				{: System.out.println("Titulo: " + list); :}
				;

c_link ::=
				link cllink
				;

c_spam ::=
				spam in:list clspam
					{:
						System.out.println("Spam: " + list);
					:}
				;

c_h1 ::=
				h1 in:list clh1
					{:
						System.out.println("h1: " + list);
					:}
				;

c_paragraph ::=
				paragraph in:list clparagraph
					{: System.out.println("paragraph: " + list); :}
				;

c_input ::=
				input clinput
				;

c_txtarea ::=
				txtarea cltxtarea;

c_button ::=
				button in:list clbutton
					{: System.out.println("button: " + list); :}
				;

c_select ::=
				select make_option clselect
				;

make_option ::=
				make_option c_option
				|
				;

c_option ::=
				option in:list cloption
					{: System.out.println("option: " + list); :}
				;

c_img ::=
				img climg
				;

c_div ::=
				div body_opt cldiv;

/* contet for p and h1 */
in ::=
			in:list str:s
				{:
					list.add(s);
					RESULT = list;
				:}
			| /* lambda */
				{:
					List<Token> list = new ArrayList<>();
					RESULT = list;
				:}
			;

str ::=
			IN:s {: RESULT = s; :}
			| ID_V:s {: RESULT = s; :}
			;

/* Etiqueta de apertura <C_GCIC> */
gcic ::=
				SMALLER GCIC params GREATER
				| error GCIC params GREATER
				| SMALLER error params GREATER
				// | SMALLER GCIC params error
				;

/* <C_HEAD> */
head ::=
				SMALLER HEAD GREATER
				| error HEAD GREATER
				| SMALLER error GREATER
				| SMALLER HEAD error
				;

/* <C_TITLE> */
title ::=
				SMALLER TITLE GREATER
				| error TITLE GREATER
				// | SMALLER error GREATER
				| SMALLER TITLE error
				;

/* <C_LINK> */
link ::=
				SMALLER LINK params GREATER
				| error LINK params GREATER
				// | SMALLER error params GREATER
				// | SMALLER LINK params error
				;

/* <C_BODY> */
body ::=
				SMALLER BODY params GREATER
				| error BODY params GREATER
				| SMALLER error params GREATER
				// | SMALLER BODY params error
				;

/* <C_SPAM> */
spam ::=
				SMALLER SPAM params GREATER
				| error SPAM params GREATER
				// | SMALLER error params GREATER
				// | SMALLER SPAM params error
				;

/* <C_INPUT> */
input ::=
				SMALLER INPUT params GREATER
				| error INPUT params GREATER
				// | SMALLER error params GREATER
				// | SMALLER INPUT params error
				;

/* <C_TEXTAREA> */
txtarea ::=
				SMALLER TXTAREA params GREATER
				| error TXTAREA params GREATER
				// | SMALLER error params GREATER
				// | SMALLER TXTAREA params error
				;

/* <C_SELECT> */
select ::=
				SMALLER SELECT params GREATER
				| error SELECT params GREATER
				// | SMALLER error params GREATER
				// | SMALLER SELECT params error
				;

/* <C_OPTION> */
option ::=
				SMALLER OPTION GREATER
				| error OPTION GREATER
				| SMALLER error GREATER
				| SMALLER OPTION error
				;

/* <C_DIV> */
div ::=
				SMALLER DIV params GREATER
				| error DIV params GREATER
				// | SMALLER error params GREATER
				// | SMALLER DIV params error
				;

/* <C_IMG> */
img ::=
				SMALLER IMG params GREATER
				| error IMG params GREATER
				// | SMALLER error params GREATER
				// | SMALLER IMG params error
				;

/* <C_BR> */
br ::=
				SMALLER BR GREATER
				| error BR GREATER
				// | SMALLER error GREATER
				| SMALLER BR error
				;

/* <C_BUTTON> */
button ::=
				SMALLER BUTTON params GREATER
				| error BUTTON params GREATER
				// | SMALLER error params GREATER
				// | SMALLER BUTTON params error
				;

/* <C_H1> */
h1 ::=
				SMALLER H1 params GREATER
				| error H1 params GREATER
				| SMALLER error params GREATER
				// | SMALLER H1 params error
				;

/* <C_P> */
paragraph ::=
				SMALLER PARAGRAPH params GREATER
				| error PARAGRAPH params GREATER
				// | SMALLER error params GREATER
				// | SMALLER PARAGRAPH params error
				;

/* <C_SCRIPTING> */
script ::=
				SMALLER SCRIPT GREATER
				| error SCRIPT GREATER
				;

/* parametros de las etiquetas */
params ::=
				params param
				|
				;

param ::=
				LBRACKET opt RBRACKET
				| error opt RBRACKET
				| LBRACKET opt error
				;

opt ::=
				id
				| name
				| href_src
				| bckgrnd_clr
				| font_size
				| font_family
				| text_align
				| type
				| cols_rows
				| clss
				| width_height
				| alt
				| onclick
				| error
				;

id ::=
				ID EQUAL ID_:s
				{: System.out.println("id: " + s); :}
				| error EQUAL ID_
				| ID error ID_
				| ID EQUAL error
				;

name ::=
				NAME EQUAL STRING:s
				{: System.out.println("name = " + s); :}
				| error EQUAL STRING
				| NAME error STRING
				| NAME EQUAL error
				;

/* href/src */
href_src ::=
				url:p EQUAL URL:s
					{: System.out.println(p + ": " + s); :}
				| error EQUAL URL
				| url error URL
				| url EQUAL error
				;

/* href/src */
url ::=
				HREF
					{: RESULT = "href"; :}
				| SRC
					{: RESULT = "src"; :}
				;

/* background / color */
bckgrnd_clr ::=
				color EQUAL COLOUR:s
					{: System.out.println("Color: " + s); :}
				| error EQUAL COLOUR
				| color error COLOUR
				| color EQUAL error
				;

/* background / color */
color ::=
				BCKGRND
					{: RESULT = "background"; :}
				| COLOR
					{: RESULT = "color"; :}
				;

font_size ::=
				FONTS EQUAL PIXEL:s
					{: System.out.println("font-size: " + s); :}
				// | error EQUAL PIXEL
				| FONTS error PIXEL
				| FONTS EQUAL error
				;

font_family ::=
				FONTF EQUAL FONTF_VALUE:s
					{: System.out.println("font-family: " + s); :}
				| error EQUAL FONTF_VALUE
				| FONTF error FONTF_VALUE
				| FONTF EQUAL error
				;

text_align ::=
				ALIGN EQUAL ALIGN_VALUE:s
					{: System.out.println("align: " + s); :}
				| error EQUAL ALIGN_VALUE
				| ALIGN error ALIGN_VALUE
				| ALIGN EQUAL error
				;

/* type for inputs */
type ::=
				TYPE EQUAL TYPE_VALUE:s
					{: System.out.println("type: " + s); :}
				| error EQUAL TYPE_VALUE
				| TYPE error TYPE_VALUE
				| TYPE EQUAL error
				;

cols_rows ::=
				cls_rws:p EQUAL INTQ:s
					{: System.out.println(p + ": " + s); :}
				| error EQUAL INTQ
				| cls_rws error INTQ
				| cls_rws EQUAL error
				;

cls_rws ::=
				COLS
					{: RESULT = "cols"; :}
				| ROWS
					{: RESULT = "rows"; :}
				;

clss ::=
				CLASS EQUAL CLASS_VALUE:s
					{: System.out.println("class: " + s); :}
				| error EQUAL CLASS_VALUE
				| CLASS error CLASS_VALUE
				| CLASS EQUAL error
				;

/* width/height */
width_height ::=
				w_h EQUAL pxl_per:s
					{: System.out.println("width/height: " + s); :}
				| error EQUAL pxl_per
				| w_h error pxl_per
				| w_h EQUAL error
				;

w_h ::=
				WIDTH
				| HEIGHT
				;

pxl_per ::=
				PIXEL:s
					{: RESULT = s; :}
				| PERCNTG:s
					{: RESULT = s; :}
				;

/* alt */
alt ::=
				ALT EQUAL STRING:s
					{: System.out.println("alt: " + s); :}
				// | error EQUAL STRING // alt ::= error ...  and name ::= error ...
				| ALT error STRING
				| ALT EQUAL error
				;

/* onclick */
onclick ::=
				CLICK EQUAL ONCLICK:s
					{: System.out.println("onclick: " + s); :}
				| error EQUAL ONCLICK
				| CLICK error ONCLICK
				| CLICK EQUAL error
				;
/* --------------- Etiquetas de cierre --------------- */
/* </C_GCIC> */
clgcic ::=
				SMALLER DIVIDE GCIC GREATER
				| error DIVIDE GCIC GREATER
				| SMALLER error GCIC GREATER
				| SMALLER DIVIDE error GREATER
				| SMALLER DIVIDE GCIC error
				;

/* </C_HEAD> */
clhead ::=
				SMALLER DIVIDE HEAD GREATER
				| error DIVIDE HEAD GREATER
				| SMALLER error HEAD GREATER
				| SMALLER DIVIDE error GREATER
				| SMALLER DIVIDE HEAD error
				;

/* </C_TITLE> */
cltitle ::=
				SMALLER DIVIDE TITLE GREATER
				| error DIVIDE TITLE GREATER
				| SMALLER error TITLE GREATER
				| SMALLER DIVIDE error GREATER
				| SMALLER DIVIDE TITLE error
				;

/* </C_LINK> */
cllink ::=
				SMALLER DIVIDE LINK GREATER
				| error DIVIDE LINK GREATER
				| SMALLER error LINK GREATER
				| SMALLER DIVIDE error GREATER
				| SMALLER DIVIDE LINK error
				;

/* </C_BODY> */
clbody ::=
				SMALLER DIVIDE BODY GREATER
				| error DIVIDE BODY GREATER
				| SMALLER error BODY GREATER
				| SMALLER DIVIDE error GREATER
				| SMALLER DIVIDE BODY error
				;

/* </C_SPAM> */
clspam ::=
				SMALLER DIVIDE SPAM GREATER
				| error DIVIDE SPAM GREATER
				| SMALLER error SPAM GREATER
				| SMALLER DIVIDE error GREATER
				| SMALLER DIVIDE SPAM error
				;

/* </C_INPUT> */
clinput ::=
				SMALLER DIVIDE INPUT GREATER
				| error DIVIDE INPUT GREATER
				| SMALLER error INPUT GREATER
				| SMALLER DIVIDE error GREATER
				| SMALLER DIVIDE INPUT error
				;

/* </C_TEXTAREA> */
cltxtarea ::=
				SMALLER DIVIDE TXTAREA GREATER
				| error DIVIDE TXTAREA GREATER
				| SMALLER error TXTAREA GREATER
				| SMALLER DIVIDE error GREATER
				| SMALLER DIVIDE TXTAREA error
				;

/* </C_SELECT> */
clselect ::=
				SMALLER DIVIDE SELECT GREATER
				| error DIVIDE SELECT GREATER
				| SMALLER error SELECT GREATER
				| SMALLER DIVIDE error GREATER
				| SMALLER DIVIDE SELECT error
				;

/* </C_OPTION> */
cloption ::=
				SMALLER DIVIDE OPTION GREATER
				| error DIVIDE OPTION GREATER
				| SMALLER error OPTION GREATER
				| SMALLER DIVIDE error GREATER
				| SMALLER DIVIDE OPTION error
				;

/* </C_DIV> */
cldiv ::=
				SMALLER DIVIDE DIV GREATER
				| error DIVIDE DIV GREATER
				| SMALLER error DIV GREATER
				| SMALLER DIVIDE error GREATER
				| SMALLER DIVIDE DIV error
				;

/* </C_IMG> */
climg ::=
				SMALLER DIVIDE IMG GREATER
				| error DIVIDE IMG GREATER
				| SMALLER error IMG GREATER
				| SMALLER DIVIDE error GREATER
				| SMALLER DIVIDE IMG error
				;

/* </C_BUTTON> */
clbutton ::=
				SMALLER DIVIDE BUTTON GREATER
				| error DIVIDE BUTTON GREATER
				| SMALLER error BUTTON GREATER
				| SMALLER DIVIDE error GREATER
				| SMALLER DIVIDE BUTTON error
				;

/* </C_H1> */
clh1 ::=
				SMALLER DIVIDE H1 GREATER
				| error DIVIDE H1 GREATER
				| SMALLER error H1 GREATER
				| SMALLER DIVIDE error GREATER
				| SMALLER DIVIDE H1 error
				;

/* </C_P> */
clparagraph ::=
				SMALLER DIVIDE PARAGRAPH GREATER
				| error DIVIDE PARAGRAPH GREATER
				| SMALLER error PARAGRAPH GREATER
				| SMALLER DIVIDE error GREATER
				| SMALLER DIVIDE PARAGRAPH error
				;

/* <C_SCRIPTING> */
clscript ::=
				SMALLER DIVIDE SCRIPT GREATER
				| error DIVIDE SCRIPT GREATER
				| SMALLER error SCRIPT GREATER
				| SMALLER DIVIDE error GREATER
				| SMALLER DIVIDE SCRIPT error
				;
